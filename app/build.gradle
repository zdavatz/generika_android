buildscript {
  /**
   * The nested repositories{} block declares that this build uses the
   * jcenter repository.
   */
  repositories {
    jcenter()
    google()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }

  /**
   * This block declares a dependency on the 3.2.1 version
   * of the Gradle plugin for the buildscript.
   */
  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.1'
    classpath "io.realm:realm-gradle-plugin:10.15.1"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
  signingConfigs {
    release
  }
  namespace "org.oddb.generika"

  compileSdkVersion 34

  defaultConfig {
    applicationId "org.oddb.generika"
    versionCode 60

    versionName "1.0.60"

    // Android 6.0 - 8.1
    minSdkVersion 23
    targetSdkVersion 34

    // junit4
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  // https://google.github.io/android-gradle-dsl/current/ \
  //   com.android.build.gradle.internal.dsl.LintOptions.html
  lintOptions {
    // check but allow bulding for warnings
    checkReleaseBuilds true
    abortOnError false
  }

  buildFeatures {
    buildConfig = true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'),
        'proguard-rules.pro'
      signingConfig signingConfigs.release
    }

    debug {
      debuggable true
      minifyEnabled false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  applicationVariants.all { variant ->
    variant.resValue "string", "versionName", variant.versionName
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

tasks.withType(Test) {
  testLogging {
    events "standardOut", "started", "passed", "skipped", "failed"
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  // -- support:
  // https://developer.android.com/topic/libraries/support-library/ \
  //   packages.html
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation 'com.google.android.material:material:1.0.0'
  implementation 'androidx.appcompat:appcompat:1.0.0'
  implementation 'androidx.preference:preference:1.0.0'
  implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'androidx.legacy:legacy-support-v13:1.0.0'
  implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
  implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
  implementation 'androidx.fragment:fragment:1.0.0'

  // -- dependency
  implementation 'com.daimajia.swipelayout:library:1.2.0@aar'
  implementation 'com.google.android.gms:play-services-vision:11.8.0'
  implementation 'com.github.bumptech.glide:glide:4.10.0'
  implementation 'io.realm:realm-gradle-plugin:10.15.1'
  implementation 'androidx.test:core:1.5.0'
  implementation 'org.dom4j:dom4j:2.1.4'

  annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

  // -- testing
  // src/test
  testImplementation 'junit:junit:4.13.2'
  testImplementation "org.robolectric:robolectric:4.10.3"
  testImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation 'io.reactivex.rxjava2:rxjava:2.1.5'

  // NOTE:
  // Appearently, Realm and Robolectric don't work together right now :'(
  //
  // - https://github.com/robolectric/robolectric/issues/1389
  // - https://github.com/realm/realm-java/issues/3808

  // src/androidTest
  androidTestImplementation 'androidx.test:runner:1.5.2'
  androidTestImplementation 'androidx.test:rules:1.5.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

if (!project.hasProperty('isTest')) {
  def Properties props = new Properties()
  def propFile = new File('signing.properties')
  if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props!=null &&
      props.containsKey('STORE_FILE') &&
      props.containsKey('STORE_PASSWORD') &&
      props.containsKey('KEY_ALIAS') &&
      props.containsKey('KEY_PASSWORD')) {
      android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
      android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
      android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
      android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
      println 'signing.properties found but some entries are missing'
      android.buildTypes.release.signingConfig = null
    }
  } else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
  }
}
